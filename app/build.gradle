plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}
apply from: "../configOutput.gradle"

def keystorePropertiesFile = rootProject.file("app/keystore.properties")

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    flavorDimensions "versionCode"
    defaultConfig {
        applicationId "com.example.surveysapp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file(getProjectDir().getPath() + '/debugKey.jks')
            keyAlias 'androidDebug'
            keyPassword '123456'
            storePassword '123456'
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(getProjectDir().getPath() + "/" + keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        staging {
            applicationId "com.example.surveysapp.staging"
            versionCode rootProject.ext.versionCodeDev
            versionName rootProject.ext.versionName
            buildConfigField 'String', 'client_id', stg_client_id
            buildConfigField 'String', 'client_secret', stg_client_secret
            buildConfigField 'String', 'BASE_URL', '"https://nimble-survey-web-staging.herokuapp.com/"'
        }

        product {
            applicationId "com.example.surveysapp.android"
            versionCode rootProject.ext.versionCode
            versionName rootProject.ext.versionName
            buildConfigField 'String', 'client_id', client_id
            buildConfigField 'String', 'client_secret', client_secret
            buildConfigField 'String', 'BASE_URL', '"https://survey-api.nimblehq.co/"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Fragment KTX
    implementation 'androidx.fragment:fragment-ktx:1.3.4'

    // Viewpager 2
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Coroutine Lifecycle Scopes
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$life_cycle_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$fragment_component_version"
    implementation "androidx.navigation:navigation-ui-ktx:$fragment_component_version"

    // Coroutine Image Loader
    implementation "io.coil-kt:coil:1.1.1"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_v4_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_compiler_version"

    // Room Database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    //Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
}